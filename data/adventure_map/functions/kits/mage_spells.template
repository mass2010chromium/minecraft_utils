# Splash melee
tag @a[tag=mage,scores={use.golden_hoe=1}] add mage_splash
execute as @a[tag=mage_splash] at @s positioned ^ ^ ^2 anchored eyes run 
    scoreboard players operation @e[distance=0..3,type=!minecraft:player,scores={fine_hp.hp=1..},limit=3,sort=nearest] ##magic_damage## 
        += @s ##mage_attack_splash_power##
tag @a remove mage_splash
scoreboard players set @a[tag=mage,scores={use.golden_hoe=1}] ##t0## 5
execute as @a[tag=mage,scores={use.golden_hoe=1}] run scoreboard players operation @s ##t0## *= @s ##mp_regen##
execute as @a[tag=mage,scores={use.golden_hoe=1}] run scoreboard players operation @s ##mp## += @s ##t0##
scoreboard players set @a use.golden_hoe 0

# Replenish items
tag @e[type=minecraft:item,nbt={Item:{tag:{display:{Lore:[##mage_melee##]}}}},tag=!processed] add replenish_0
tag @e[type=minecraft:item,nbt={Item:{tag:{display:{Lore:[##mage_primary##]}}}},tag=!processed] add replenish_1
tag @e[type=minecraft:item,nbt={Item:{tag:{display:{Lore:[##mage_secondary##]}}}},tag=!processed] add replenish_2

execute as @e[tag=replenish_0] at @s run tag @a[distance=0..4,nbt={Inventory:[{Slot:0b}]},tag=mage] add replenish_fail
execute as @e[tag=replenish_1] at @s run tag @a[distance=0..4,nbt={Inventory:[{Slot:1b}]},tag=mage] add replenish_fail
execute as @e[tag=replenish_2] at @s run tag @a[distance=0..4,nbt={Inventory:[{Slot:2b}]},tag=mage] add replenish_fail

# Weapon drop protection
execute as @e[tag=replenish_0,nbt={Item:{tag:{display:{Name:##mage_basic_staff_name##}}}}] at @s store success score @s replenish_ok run 
    replaceitem entity @p[distance=0..4,nbt={SelectedItemSlot:0},tag=!replenish_fail,tag=mage] container.0 ##mage_basic_staff_item##
execute as @e[tag=replenish_0,nbt={Item:{tag:{display:{Name:##mage_fireball_staff_name##}}}}] at @s store success score @s replenish_ok run 
    replaceitem entity @p[distance=0..4,nbt={SelectedItemSlot:0},tag=!replenish_fail,tag=mage] container.0 ##mage_fireball_staff_item##


# Fireball spell
execute as @e[tag=replenish_1,nbt={Item:{tag:{display:{Name:##mage_fireball_1_name##}}}}] at @s store success score @s replenish_ok run 
    replaceitem entity @p[distance=0..4,nbt={SelectedItemSlot:1},tag=!replenish_fail,tag=mage] container.1 ##mage_fireball_1_item##
execute as @e[tag=replenish_1,nbt={Item:{tag:{display:{Name:##mage_fireball_2_name##}}}}] at @s store success score @s replenish_ok run 
    replaceitem entity @p[distance=0..4,nbt={SelectedItemSlot:1},tag=!replenish_fail,tag=mage] container.1 ##mage_fireball_2_item##
    
scoreboard players set @a[tag=mage] ##t0## 0
# Trigger fireball summon, and remove mana from player
execute as @e[tag=replenish_1,nbt={Item:{tag:{display:{Lore:[##mage_fireball_spell##]}}}},scores={replenish_ok=1}] run
    execute at @s run
    execute at @p[tag=mage,nbt={SelectedItemSlot:1},tag=!replenish_fail,distance=0..4,scores={##mp##=200..}] 
        store success score @p[tag=mage,nbt={SelectedItemSlot:1},tag=!replenish_fail,distance=0..4,scores={##mp##=200..}]
            ##t0##
    run tag @s add fireball_summon
    
tag @a[tag=mage,scores={##t0##=1}] add fireball_summoner
scoreboard players remove @a[tag=fireball_summoner] ##mp## 200
execute as @a[tag=fireball_summoner] at @s run 
    summon minecraft:fireball ^ ^ ^0.1 {direction:[0.0,0.0,0.0],ExplosionPower:0,Tags:["unprocessed","mage_fireball"]}
execute at @e[tag=fireball_summoner] run playsound minecraft:entity.blaze.shoot master @a[distance=0..8] ~ ~ ~

# Record source x,y,z, and assign damage
execute as @e[tag=fireball_summoner] at @s store result score @e[tag=mage_fireball,sort=nearest,limit=1,tag=unprocessed] ##t1## run 
        data get entity @s Pos[0] 1000
execute as @e[tag=fireball_summoner] at @s store result score @e[tag=mage_fireball,sort=nearest,limit=1,tag=unprocessed] ##t2## run 
        data get entity @s Pos[1] 1000
execute as @e[tag=fireball_summoner] at @s store result score @e[tag=mage_fireball,sort=nearest,limit=1,tag=unprocessed] ##t3## run 
        data get entity @s Pos[2] 1000
execute as @e[tag=fireball_summoner] at @s run 
    scoreboard players operation @e[tag=mage_fireball,sort=nearest,limit=1,tag=unprocessed] ##mage_fireball_power## = @s ##mage_fireball_power##

# Start doing vector math
# t0, t1, t2 are dx, dy, dz
execute as @e[tag=unprocessed,tag=mage_fireball] store result score @s ##t0## run data get entity @s Pos[0] 1000
execute as @e[tag=unprocessed,tag=mage_fireball] run scoreboard players operation @s ##t0## -= @s ##t1##
execute as @e[tag=unprocessed,tag=mage_fireball] store result score @s ##t1## run data get entity @s Pos[1] 1000
execute as @e[tag=unprocessed,tag=mage_fireball] run scoreboard players operation @s ##t1## -= @s ##t2##
scoreboard players add @e[tag=unprocessed,tag=mage_fireball] ##t1## 10
execute as @e[tag=unprocessed,tag=mage_fireball] store result score @s ##t2## run data get entity @s Pos[2] 1000
execute as @e[tag=unprocessed,tag=mage_fireball] run scoreboard players operation @s ##t2## -= @s ##t3##

execute as @e[tag=unprocessed,tag=mage_fireball] at @s run tp @s ~ ~1.5 ~
# Finally store back into entity data.
execute as @e[tag=mage_fireball] store result entity @s direction[0] double 0.03 run scoreboard players get @s ##t0##
execute as @e[tag=unprocessed,tag=mage_fireball] store result entity @s direction[1] double 0.03 run scoreboard players get @s ##t1##
execute as @e[tag=mage_fireball] store result entity @s direction[2] double 0.03 run scoreboard players get @s ##t2##

execute as @e[tag=unprocessed,tag=mage_fireball] run data modify entity @s power[1] set value -0.25
scoreboard players set @e[tag=unprocessed,tag=mage_fireball] ##t3## 10
scoreboard players remove @e[tag=mage_fireball] ##t3## 1
kill @e[tag=mage_fireball,scores={##t3##=0}]
    
execute as @e[tag=fireball_summon] run tag @e[type=minecraft:fireball,sort=nearest,limit=1,tag=unprocessed] remove unprocessed
execute at @e[tag=mage_fireball] run particle flame ~ ~ ~ 0 0 0 0.05 5

execute at @e[tag=mage_fireball] run kill @e[tag=mage_fireball_tracker,limit=1,sort=nearest]
execute as @e[tag=mage_fireball_tracker] at @s run scoreboard players operation @e[distance=0..3] ##magic_damage## += @s ##mage_fireball_power##
execute as @e[tag=mage_fireball_tracker] at @s run scoreboard players operation @e[distance=0..5] ##magic_damage## += @s ##mage_fireball_power##
execute at @e[tag=mage_fireball_tracker] run particle minecraft:explosion_emitter ~ ~ ~ 0 0 0 0 1 force

execute at @e[tag=mage_fireball] run summon minecraft:area_effect_cloud ~ ~ ~ {Duration:2s,Tags:["mage_fireball_tracker"]}
execute as @e[tag=mage_fireball] at @s run 
    scoreboard players operation @e[distance=0..2,sort=nearest,tag=mage_fireball_tracker] ##mage_fireball_power## = @s ##mage_fireball_power##

tag @e[tag=unprocessed] remove unprocessed
tag @a[tag=fireball_summoner] remove fireball_summoner


# Zephyr spell
execute as @e[tag=replenish_2,nbt={Item:{tag:{display:{Name:##mage_zephyr_1_name##}}}}] at @s store success score @s replenish_ok run 
    replaceitem entity @p[distance=0..4,nbt={SelectedItemSlot:2},tag=!replenish_fail,tag=mage] container.2 ##mage_zephyr_1_item##

scoreboard players set @a[tag=mage] ##t0## 0
# Tag with zephyr speed boost, and remove mana from player
execute as @e[tag=replenish_2,nbt={Item:{tag:{display:{Lore:[##mage_zephyr_spell##]}}}},scores={replenish_ok=1}] run
    execute at @s run
    execute at @p[tag=mage,nbt={SelectedItemSlot:2},tag=!replenish_fail,distance=0..4,scores={##mp##=500..}] 
        store success score @p[tag=mage,nbt={SelectedItemSlot:2},tag=!replenish_fail,distance=0..4,scores={##mp##=500..}]
            ##t0##
    run tag @s add zephyr_boost
scoreboard players remove @a[tag=mage,scores={##t0##=1}] ##mp## 500

execute as @e[tag=zephyr_boost] at @s
    run scoreboard players operation @s ##t0## = @p[distance=0..4,scores={##t0##=1},tag=mage] ##mage_zephyr_power##
    
execute at @e[tag=zephyr_boost] run playsound minecraft:block.beacon.power_select master @a[distance=0..8] ~ ~ ~
execute at @e[tag=zephyr_boost] run effect give @a[distance=0..8] minecraft:speed 80 0
execute at @e[tag=zephyr_boost] run effect give @a[distance=0..8] minecraft:strength 5 0
execute at @e[tag=zephyr_boost,scores={##t0##=1}] run effect give @a[distance=0..8] minecraft:speed 5 1
execute at @e[tag=zephyr_boost,scores={##t0##=2}] run effect give @a[distance=0..8] minecraft:speed 5 2
execute at @e[tag=zephyr_boost,scores={##t0##=3}] run effect give @a[distance=0..8] minecraft:speed 5 3
execute at @e[tag=zephyr_boost,scores={##t0##=4}] run effect give @a[distance=0..8] minecraft:speed 5 4


# Arcane Explosion spell
execute as @e[tag=replenish_2,nbt={Item:{tag:{display:{Name:##mage_repulse_1_name##}}}}] at @s store success score @s replenish_ok run 
    replaceitem entity @p[distance=0..4,nbt={SelectedItemSlot:2},tag=!replenish_fail,tag=mage] container.2 ##mage_repulse_1_item##

scoreboard players set @a[tag=mage] ##t0## 0
# Tag with arcane explosion, and remove mana from player
execute as @e[tag=replenish_2,nbt={Item:{tag:{display:{Lore:[##mage_repulse_spell##]}}}},scores={replenish_ok=1}] run
    execute at @s run
    execute at @p[tag=mage,nbt={SelectedItemSlot:2},tag=!replenish_fail,distance=0..4,scores={##mp##=400..}] 
        store success score @p[tag=mage,nbt={SelectedItemSlot:2},tag=!replenish_fail,distance=0..4,scores={##mp##=400..}]
            ##t0##
    run tag @s add arcane_explode
scoreboard players remove @a[tag=mage,scores={##t0##=1}] ##mp## 400

execute as @e[tag=arcane_explode] at @s
    run scoreboard players operation @s ##t0## = @p[distance=0..4,scores={##t0##=1},tag=mage] ##mage_repulse_power##
    
execute at @e[tag=arcane_explode] run playsound minecraft:entity.wither.spawn master @a[distance=0..8] ~ ~ ~
execute at @e[tag=arcane_explode] run particle minecraft:end_rod ~ ~ ~ 0 0 0 1 150

# Record source x and z, and deal damage
execute as @e[tag=arcane_explode] at @s store result score @e[team=##enemies##,distance=0..8] ##t1## run 
        data get entity @s Pos[0] 100
execute as @e[tag=arcane_explode] at @s store result score @e[team=##enemies##,distance=0..8] ##t2## run 
        data get entity @s Pos[2] 100
execute as @e[tag=arcane_explode] at @s run 
    scoreboard players operation @e[team=##enemies##,distance=0..7] ##magic_damage## += @s ##t0##
execute at @e[tag=arcane_explode] run tag @e[team=##enemies##,distance=0..7] add repulsed

# Start doing vector math
# t0, t1 are dx, dz
execute as @e[tag=repulsed] store result score @s ##t0## run data get entity @s Pos[0] 100
execute as @e[tag=repulsed] run scoreboard players operation @s ##t0## -= @s ##t1##
execute as @e[tag=repulsed] store result score @s ##t1## run data get entity @s Pos[2] 100
execute as @e[tag=repulsed] run scoreboard players operation @s ##t1## -= @s ##t2##
# Calculate r^2
execute as @e[tag=repulsed] run scoreboard players operation @s ##t2## = @s ##t0##
execute as @e[tag=repulsed] run scoreboard players operation @s ##t2## *= @s ##t0##
execute as @e[tag=repulsed] run scoreboard players operation @s ##t3## = @s ##t1##
execute as @e[tag=repulsed] run scoreboard players operation @s ##t3## *= @s ##t1##
execute as @e[tag=repulsed] run scoreboard players operation @s ##t2## += @s ##t3##
# Magic number in numerator that happens to make things work nicely, tweak this in smal increments to change repulse kb
scoreboard players set @e[tag=repulsed] ##t3## 600000
# K/r^2
execute as @e[tag=repulsed] run scoreboard players operation @s ##t3## /= @s ##t2##
# Kr/r^2
execute as @e[tag=repulsed] run scoreboard players operation @s ##t0## *= @s ##t3##
execute as @e[tag=repulsed] run scoreboard players operation @s ##t1## *= @s ##t3##
# Cap out at velocity 1, technically wrong but works
scoreboard players set @e[tag=repulsed] ##t2## 1500
execute as @e[tag=repulsed] run scoreboard players operation @s ##t0## < @s ##t2##
execute as @e[tag=repulsed] run scoreboard players operation @s ##t1## < @s ##t2##
scoreboard players set @e[tag=repulsed] ##t2## -1500
execute as @e[tag=repulsed] run scoreboard players operation @s ##t0## > @s ##t2##
execute as @e[tag=repulsed] run scoreboard players operation @s ##t1## > @s ##t2##
# Finally store back into entity data.
execute as @e[tag=repulsed] store result entity @s Motion[0] double 0.001 run scoreboard players get @s ##t0##
execute as @e[tag=repulsed] run data modify entity @s Motion[1] set value 0.5
execute as @e[tag=repulsed] store result entity @s Motion[2] double 0.001 run scoreboard players get @s ##t1##
tag @e[tag=repulsed] remove repulsed

kill @e[scores={replenish_ok=1},type=!trident]

tag @e[tag=replenish_0] add processed
tag @e[tag=replenish_1] add processed
tag @e[tag=replenish_2] add processed
tag @e[tag=replenish_0] remove replenish_0
tag @e[tag=replenish_1] remove replenish_1
tag @e[tag=replenish_2] remove replenish_2
tag @a remove replenish_fail

# Apply buffs
scoreboard players set @a[tag=mage] ##mage_fireball_power## 4
scoreboard players add @a[tag=mage,nbt={Inventory:[{Slot:0b,tag:{display:{Lore:[##mage_fireball_boost_1##]}}}]}] ##mage_fireball_power## 5
scoreboard players add @a[tag=mage,nbt={Inventory:[{Slot:1b,tag:{display:{Lore:[##mage_fireball_boost_1##]}}}]}] ##mage_fireball_power## 5
scoreboard players add @a[tag=mage,nbt={Inventory:[{Slot:2b,tag:{display:{Lore:[##mage_fireball_boost_1##]}}}]}] ##mage_fireball_power## 5

scoreboard players set @a[tag=mage] ##mage_repulse_power## 0
scoreboard players add @a[tag=mage,nbt={Inventory:[{Slot:0b,tag:{display:{Lore:[##mage_repulse_boost_1##]}}}]}] ##mage_repulse_power## 1
scoreboard players add @a[tag=mage,nbt={Inventory:[{Slot:1b,tag:{display:{Lore:[##mage_repulse_boost_1##]}}}]}] ##mage_repulse_power## 1
scoreboard players add @a[tag=mage,nbt={Inventory:[{Slot:2b,tag:{display:{Lore:[##mage_repulse_boost_1##]}}}]}] ##mage_repulse_power## 1

scoreboard players set @a[tag=mage] ##mage_zephyr_power## 1
scoreboard players add @a[tag=mage,nbt={Inventory:[{Slot:0b,tag:{display:{Lore:[##mage_fireball_boost_1##]}}}]}] ##mage_zephyr_power## 1
scoreboard players add @a[tag=mage,nbt={Inventory:[{Slot:1b,tag:{display:{Lore:[##mage_fireball_boost_1##]}}}]}] ##mage_zephyr_power## 1
scoreboard players add @a[tag=mage,nbt={Inventory:[{Slot:2b,tag:{display:{Lore:[##mage_fireball_boost_1##]}}}]}] ##mage_zephyr_power## 1

scoreboard players set @a[tag=mage] ##mage_attack_splash_power## 2
scoreboard players add @a[tag=mage,nbt={Inventory:[{Slot:0b,tag:{display:{Lore:[##mage_splash_power_boost_2##]}}}]}] ##mage_attack_splash_power## 2
scoreboard players add @a[tag=mage,nbt={Inventory:[{Slot:1b,tag:{display:{Lore:[##mage_splash_power_boost_2##]}}}]}] ##mage_attack_splash_power## 2
scoreboard players add @a[tag=mage,nbt={Inventory:[{Slot:2b,tag:{display:{Lore:[##mage_splash_power_boost_2##]}}}]}] ##mage_attack_splash_power## 2